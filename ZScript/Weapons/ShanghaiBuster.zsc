class ShanghaiBuster : MegaBuster
{
	Default
	{
		Tag "Shanghai Buster";
	}
	States
	{
		Ready:
			NNSB A 1 A_WeaponReady;
			Loop;
		Deselect:
			NNSB A 1 A_Lower;
			Loop;
		Select:
			NNSB A 1 A_Raise;
			Loop;
		Fire:
			NNSB A 0 {
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
			}
			Charge:
			NNSB A 1 A_GiveInventory("PrimaryCharge", 1);
			NNSB A 0 A_Refire("Charge");
			NNSB A 0 A_JumpIfInventory("PrimaryCharge", 50, "FireChargeShot");
			Goto FireNormalShot;
		AltFire:
		FireNormalShot:
			NNSB A 1 A_SetInventory("PrimaryCharge", 0);
			NNSB B 1 Bright {
				//A_FireBullets(2.85, 2.85, 1, 3, "UnchargedBusterPuff", FBF_NORANDOM, 8192, "UnchargedBusterTracerSH", 0, 4);
				A_FireProjectile("UnchargedBusterTracerSH", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/sh-pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			NNSB C 1 Bright;
			NNSB D 1 Bright;
			NNSB E 1;
			Goto Ready;
		FireChargeShot:
			NNSB A 1 A_SetInventory("PrimaryCharge", 0);
			NNSB B 1 Bright {
//				A_FireBullets(2.85, 2.85, 1, 30, "MaxChargeBusterPuff", FBF_NORANDOM, 8192, "MaxChargeBusterTracerSH", 0, 4);
				A_FireProjectile("MaxChargeBusterTracerSH", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/sh-pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			NNSB C 1 Bright;
			NNSB D 1 Bright;
			NNSB E 1;
			Goto Ready;
		Spawn:
			SHTG A -1;
			Stop;
			
			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",50,"ChargeSoundFunction");
			//Upon reaching 99 charge, play the charged sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",49,"ChargeSound2");
			//With 5 or more, stay silent.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",3,"ChargeSound1");
			//If there's any charge, restart.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;
			
			ChargeSound1:
				TNT1 A 0 A_PlaySound("weapons/buster/sh-charging", CHAN_WEAPON);
				goto ChargeSoundFunction;
			
			ChargeSound2:
				TNT1 A 0 A_PlaySound("weapons/buster/sh-charged", CHAN_WEAPON);
				goto ChargeSoundFunction;
		
			// Subroutine for charge effect
		ChargeEffectFunction:
			TNT1 A 1;
			// if charge is full, play fully-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",50,"FullChargeAnim");
			// if charge is starting, play not-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"StartChargeAnim");
			stop;

			FullChargeAnim:
				BCH2 IJKLMNOP 2 Bright;
				goto ChargeEffectFunction;

			StartChargeAnim:
				BCH2 ABCDEFGH 2 Bright;
				goto ChargeEffectFunction;
	}
}

class UnchargedBusterTracerSH : UnchargedBusterTracer
{
	Default
	{
		DeathSound "weapons/shanghit";
	}
	States
	{
		Spawn:
			BNTR D 1 Bright;
			Loop;
		Death:
			TNT1 A 0 A_SpawnItemEx("UnchargedBusterPuffSH");
			Stop;
	}
}

class MaxChargeBusterTracerSH : UnchargedBusterTracerSH
{
	States
	{
		Spawn:
			BNTR E 1 Bright;
			Loop;
		Death:
			TNT1 A 0 A_SpawnItemEx("MaxChargeBusterPuffSH");
			Stop;
	}
}

class UnchargedBusterPuffSH : BusterPuffBase
{
	States
	{
		Spawn:
			BNP3 ABCD 1 Bright;
			Stop;
	}
}

class MaxChargeBusterPuffSH : BusterPuffBase
{
	States
	{
		Spawn:
			BNP4 AB 1 Bright;
			BNP4 CDEF 2 Bright;
			Stop;
	}
}