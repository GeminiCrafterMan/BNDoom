class PrimaryCharge : Inventory
{
	Default
	{
		Inventory.MaxAmount 100;
	}
}

class ChargingSoundPlayed : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

class ChargedSoundPlayed : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

class MegaBuster : Weapon
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Obituary "Test obituary: %o killed %k.";
		+WEAPON.WIMPY_WEAPON;
		Inventory.Pickupmessage "You shouldn't be seeing this.";
		Tag "MegaBuster";
//		AttackSound "weapons/buster/pew";
	}
	States
	{
		Ready:
			NBST A 1 A_WeaponReady;
			Loop;
		Deselect:
			NBST A 1 A_Lower;
			Loop;
		Select:
			TNT1 A 0 A_OverlayTranslation(OverlayID(), "MMNormStyl");	// Set translation
			NBST A 1 A_Raise;
			Loop;
		Fire:
			NBST A 0 {
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
			}
			Charge:
			NBST A 1 A_GiveInventory("PrimaryCharge", 1);
			NBST A 0 A_Refire("Charge");
			NBST A 0 A_JumpIfInventory("PrimaryCharge", 100, "FireChargeShot");
			NBST A 0 A_JumpIfInventory("PrimaryCharge", 50, "FireMidChargeShot");
			Goto FireNormalShot;
		AltFire:
		FireNormalShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST B 1 Bright {
				//A_FireBullets(2.85, 2.85, 1, 3, "UnchargedBusterPuff", FBF_NORANDOM, 8192, "UnchargedBusterTracer", 0, 4);
				A_FireProjectile("UnchargedBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			NBST C 1 Bright;
			NBST D 1 Bright;
			NBST E 1;
			Goto Ready;
		FireMidChargeShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST B 1 Bright {
//				A_FireBullets(2.85, 2.85, 1, 15, "MidChargeBusterPuff", FBF_NORANDOM, 8192, "MidChargeBusterTracer", 0, 4);
				A_FireProjectile("MidChargeBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			NBST C 1 Bright;
			NBST D 1 Bright;
			NBST E 1;
			Goto Ready;
		FireChargeShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST B 1 Bright {
//				A_FireBullets(2.85, 2.85, 1, 30, "MaxChargeBusterPuff", FBF_NORANDOM, 8192, "MaxChargeBusterTracer", 0, 4);
				A_FireProjectile("MaxChargeBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			NBST C 1 Bright;
			NBST D 1 Bright;
			NBST E 1;
			Goto Ready;
		Spawn:
			SHTG A -1;
			Stop;
			
			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",100,"ChargeSoundFunction");
			//Upon reaching 99 charge, play the charged sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",99,"ChargeSound2");
			//With 5 or more, stay silent.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",3,"ChargeSound1");
			//If there's any charge, restart.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;
			
			ChargeSound1:
				TNT1 A 0 A_PlaySound("weapons/buster/charging", CHAN_WEAPON);
				goto ChargeSoundFunction;
			
			ChargeSound2:
				TNT1 A 0 A_PlaySound("weapons/buster/charged", CHAN_WEAPON);
				goto ChargeSoundFunction;
		
			// Subroutine for charge effect
		ChargeEffectFunction:
			TNT1 A 1;
			// if charge is full, play fully-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",100,"FullChargeAnim");
			// if charge is half, play half-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",50,"HalfChargeAnim");
			// if charge is starting, play not-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"StartChargeAnim");
			stop;

			FullChargeAnim:
				BCH0 UV 1 Bright;
				BCH0 W 2 Bright;
				BCH0 XY 1 Bright;
				BCH0 Z 2 Bright;
				BCH1 AB 1 Bright;
				BCH1 C 2 Bright;
				BCH1 DE 1 Bright;
				BCH1 F 2 Bright;
				goto ChargeEffectFunction;

			HalfChargeAnim:
				BCH0 IJ 1 Bright;
				BCH0 K 2 Bright;
				BCH0 LM 1 Bright;
				BCH0 N 2 Bright;
				BCH0 OP 1 Bright;
				BCH0 Q 2 Bright;
				BCH0 RS 1 Bright;
				BCH0 T 2 Bright;
				goto ChargeEffectFunction;

			StartChargeAnim:
				BCH0 ABCDEFGH 2 Bright;
				goto ChargeEffectFunction;
	}
}

class MegaBusterTest : MegaBuster
{
	Default
	{
		Tag "MegaBuster Translation Test";
	}
	States
	{
		Select:
			TNT1 A 0 A_OverlayTranslation(OverlayID(), "MMHeatStyl");	// Set translation
			NBST A 1 A_Raise;
			Loop;
	}
}

class BusterPuffBase : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ALLOWPARTICLES
		+PUFFONACTORS
	}
	States
	{
		Spawn:
			PUFF ABCD 4 Bright;
			Stop;
	}
}

class UnchargedBusterPuff : BusterPuffBase
{
	States
	{
		Spawn:
			BNP0 ABCD 1 Bright;
			Stop;
	}
}

class MidChargeBusterPuff : BusterPuffBase
{
	States
	{
		Spawn:
			BNP1 AB 1 Bright;
			BNP1 CDEFG 2 Bright;
			Stop;
	}
}

class MaxChargeBusterPuff : BusterPuffBase
{
	States
	{
		Spawn:
			BNP2 AB 1 Bright;
			BNP2 CDEFG 2 Bright;
			Stop;
	}
}

class UnchargedBusterTracer : FastProjectile
{
	Default
	{
		Projectile;
		+RANDOMIZE
		+FORCEXYBILLBOARD
		+DONTSPLASH
		//+BLOODSPLATTER 
		+NOEXTREMEDEATH
		Damage 1;
		Radius 2;
		Height 2;
		Speed 140;
		RenderStyle "Translucent";
		Alpha 0.75;
		Scale .15;
		Obituary "%o was tickled to death by %k's uncharged buster shot.";
		DeathSound "weapons/hit";
	}
	States
	{
		Spawn:
			BNTR A 1 Bright;
			Loop;
		Death:
			TNT1 A 0 A_SpawnItemEx("UnchargedBusterPuff");
			Stop;
	}
}

class MidChargeBusterTracer : UnchargedBusterTracer
{
	Default
	{
		Scale .30;
		Damage 5;
		Radius 4;
		Height 4;
		Obituary "%o was sniped by %k's half-charged buster shot.";
	}
	States
	{
		Spawn:
			BNTR B 1 Bright;
			Loop;
		Death:
			TNT1 A 0 A_SpawnItemEx("MidChargeBusterPuff");
			Stop;
	}
}

class MaxChargeBusterTracer : UnchargedBusterTracer
{
	Default
	{
		Scale .45;
		Damage 10;
		Radius 8;
		Height 8;
		Obituary "%o was blasted by %k's fully-charged buster shot.";
	}
	States
	{
		Spawn:
			BNTR C 1 Bright;
			Loop;
		Death:
			TNT1 A 0 A_SpawnItemEx("MaxChargeBusterPuff");
			Stop;
	}
}