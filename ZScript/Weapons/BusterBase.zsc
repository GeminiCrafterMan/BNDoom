#include "ZScript/Weapons/Chips/Sword.zsc"
#include "ZScript/Weapons/Chips/ZapRing.zsc"
#include "ZScript/Weapons/Chips/Barrier.zsc"
#include "ZScript/Weapons/Chips/AirShot.zsc"
#include "ZScript/Weapons/Chips/Vulcan.zsc"
#include "ZScript/Weapons/Chips/Recovery.zsc"
#include "ZScript/Weapons/Chips/Thunder.zsc"
#include "ZScript/Weapons/Chips/ElChain.zsc"

//FLAGS
class PrimaryCharge : Inventory {Default {Inventory.MaxAmount 100;}}
class Firing : Inventory {Default {Inventory.MaxAmount 1;}}

class BusterBase : Weapon
{
	action void A_CycleElements()
	{
		// Cast and null-check the class:
		let navi = NetNaviPlayer(self);
		if (navi) {
			switch (navi.curElement) {
				case NetNaviPlayer.Elem_Extra:
					navi.curElement = NetNaviPlayer.Elem_Normal;
					break;
				default:
					navi.curElement++;
					break;
			}
		}
	}
	action void A_CycleEmotions()
	{
		// Cast and null-check the class:
		let navi = NetNaviPlayer(self);
		if (navi) {
			switch (navi.curEmotion) {
				case NetNaviPlayer.Emotion_Happy:
					navi.curEmotion = NetNaviPlayer.Emotion_Normal;
					break;
				default:
					navi.curEmotion++;
					break;
			}
		}
	}
	action void A_EmotionCheck()
	{
		// Cast and null-check the class:
		let navi = NetNaviPlayer(self);
		if (!navi)
			return;

		switch (navi.curEmotion) {
			case NetNaviPlayer.Emotion_Evil:
				A_OverlayTranslation(99, "DarkSoul");
				A_OverlayRenderStyle(99, STYLE_SUBTRACT);
				A_OverlayAlpha(99, 0.50);
				break;
			case NetNaviPlayer.Emotion_FullSynchro:
				A_OverlayTranslation(99, "FullSynchro");
				A_OverlayRenderStyle(99, STYLE_ADD);
				A_OverlayAlpha(99, 0.25);
				break;
			case NetNaviPlayer.Emotion_Angry:
				A_OverlayTranslation(99, "Anger");
				A_OverlayRenderStyle(99, STYLE_ADD);
				A_OverlayAlpha(99, 0.25);
				break;
			default:
				A_OverlayRenderStyle(99, STYLE_ADD);
				A_OverlayAlpha(99, 0);
				break;
		}
	}
	Default
	{
		Weapon.SelectionOrder 3700;
		Obituary "Test obituary: %o killed %k.";
		+WEAPON.WIMPY_WEAPON;
		+INVENTORY.UNTOSSABLE;
		Inventory.Pickupmessage "You shouldn't be seeing this.";
		Tag "Buster Base";
	}
	States
	{
		Null:
			TNT1 A 1;
			Stop;
		Spawn:	// You really shouldn't be seeing this thing on the floor.
			UNKN A -1;
			Stop;
		Ready:
			NBST A 1 A_WeaponReady(WRF_ALLOWUSER1);
			Loop;
		Select:
			TNT1 A 0 A_SetInventory("PrimaryCharge", 0);
			NBST A 1 A_Raise(12);
			Loop;
		Deselect:
			TNT1 A 0 {
				A_SetInventory("PrimaryCharge", 0);
				A_Overlay(99, "Null");
			}
			NBST A 1 A_Lower(12);
			Loop;
		Fire:
			NBST A 0 {
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
			}
			TNT1 A 0 { return ResolveState("Charge"); }
			Charge:
				NBST A 1 A_GiveInventory("PrimaryCharge", 1);
				NBST A 0 A_Refire("Charge");
				NBST A 0 A_JumpIfInventory("PrimaryCharge", 100, "FireChargeShot");
				NBST A 0 A_JumpIfInventory("PrimaryCharge", 50, "FireMidChargeShot");
				TNT1 A 0 { return ResolveState("FireNormalShot"); }
		AltFire:
		FireNormalShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST A 0 A_GiveInventory("Firing", 1);
			NBST B 1 Bright {
				A_FireProjectile("UnchargedBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_StartSound("weapons/buster/pew", CHAN_WEAPON);
				A_Overlay(-3, "Flash");	// Flash layer
			}
			Fired:
			NBST A 0 {
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_TakeInventory("Firing", 1);
			}
			NBST C 1 Bright;
			NBST D 1 Bright;
			NBST A 1;
			TNT1 A 0 { return ResolveState("Ready"); }
		FireMidChargeShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST A 0 A_GiveInventory("Firing", 1);
			NBST B 1 Bright {
				A_FireProjectile("MidChargeBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_StartSound("weapons/buster/pew", CHAN_WEAPON);
				A_Overlay(-3, "Flash");	// Flash layer
			}
			TNT1 A 0 { return ResolveState("Fired"); }

		FireNothing:
			NBST A 1;
			NBST BCD 1 Bright;
			NBST A 1;
			TNT1 A 0 { return ResolveState("Ready"); }
			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",100,"ChargeSoundFunction");
			//Upon reaching 99 charge, play chargesound3
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",99,"ChargeSound3");
			//with 50 or more, stay silent.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",50,"ChargeSoundFunction");
			//Upon reaching 49 charge, play chargesound2
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",49,"ChargeSound2");
			//With 5 or more, stay silent.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",3,"ChargeSound1");
			//If there's any charge, restart.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;

			ChargeSound1:
				TNT1 A 0 A_StartSound("weapons/buster/charging", CHAN_WEAPON);
				TNT1 A 0 { return ResolveState("ChargeSoundFunction"); }

			ChargeSound2:
				TNT1 A 0 A_StartSound("weapons/buster/midcharged", CHAN_WEAPON);
				TNT1 A 0 { return ResolveState("ChargeSoundFunction"); }

			ChargeSound3:
				TNT1 A 0 A_StartSound("weapons/buster/charged", CHAN_WEAPON);
				TNT1 A 0 { return ResolveState("ChargeSoundFunction"); }

			// Subroutine for charge effect
		ChargeEffectFunction:
			TNT1 A 1;
			// if charge is full, play fully-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",100,"FullChargeAnim");
			// if charge is half, play half-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",50,"HalfChargeAnim");
			// if charge is starting, play not-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",1,"StartChargeAnim");
			stop;

			FullChargeAnim:
				BCH0 UV 1 Bright;
				BCH0 W 2 Bright;
				BCH0 XY 1 Bright;
				BCH0 Z 2 Bright;
				BCH1 AB 1 Bright;
				BCH1 C 2 Bright;
				BCH1 DE 1 Bright;
				BCH1 F 2 Bright;
				TNT1 A 0 { return ResolveState("ChargeEffectFunction"); }

			HalfChargeAnim:
				BCH0 IJ 1 Bright;
				BCH0 K 2 Bright;
				BCH0 LM 1 Bright;
				BCH0 N 2 Bright;
				BCH0 OP 1 Bright;
				BCH0 Q 2 Bright;
				BCH0 RS 1 Bright;
				BCH0 T 2 Bright;
				TNT1 A 0 { return ResolveState("ChargeEffectFunction"); }

			StartChargeAnim:
				BCH0 ABCDEFGH 2 Bright;
				TNT1 A 0 { return ResolveState("ChargeEffectFunction"); }

		Arm:
			TNT1 A 0 A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
			TNT1 A 0 A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
			BNAR AA 1 A_OverlayOffset(OverlayID(), -2, -2, WOF_ADD);
			BNAR AA 1 A_OverlayOffset(OverlayID(), 2, 2, WOF_ADD);
			BNAR AA 2;
			TNT1 A 0 { return ResolveState("Ready"); }
		Zoom:
			#### A 10 A_CycleElements;
			TNT1 A 0 { return ResolveState("Ready"); }
		User1:
			#### A 10 A_CycleEmotions;
			TNT1 A 0 { return ResolveState("Ready"); }

		TopLayer:
			NBST A 1 {
				let pss = player.FindPSprite(OverlayID());
				let psp = player.FindPSprite(PSP_WEAPON);
				if (pss && psp)
				{
					pss.sprite = psp.sprite;
					pss.frame = psp.frame;
				}
				A_OverlayFlags(OverlayID(), PSPF_ALPHA|PSPF_RENDERSTYLE, true);
				A_EmotionCheck();
			}
			Loop;
	}
}