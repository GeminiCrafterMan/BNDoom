class PrimaryCharge : Inventory
{
	Default
	{
		Inventory.MaxAmount 100;
	}
}

class ChargingSoundPlayed : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

class ChargedSoundPlayed : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

class Buster : Weapon
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Obituary "Test obituary: %o killed %k.";
		+WEAPON.WIMPY_WEAPON;
		Inventory.Pickupmessage "You shouldn't be seeing this.";
		Tag "Buster";
	}
	States
	{
		Ready:
			MBUS A 1 A_WeaponReady;
			Loop;
		Deselect:
			MBUS A 1 A_Lower;
			Loop;
		Select:
			MBUS A 1 A_Raise;
			Loop;
		Fire:
			MBUS A 2 {
				A_TakeInventory("PrimaryCharge", 100);
				A_TakeInventory("ChargingSoundPlayed", 1);
				A_TakeInventory("ChargedSoundPlayed", 1);
			}
			MBUS A 0 A_ReFire();
			Goto Ready;
		Hold:
			MBUS A 0 A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
			Charge:
			MBUS A 1 A_GiveInventory("PrimaryCharge", 1);
			MBUS A 0 A_Refire("Charge");
			MBUS A 0 A_JumpIfInventory("PrimaryCharge", 100, "FireChargeShot");
			MBUS A 0 A_JumpIfInventory("PrimaryCharge", 50, "FireMidChargeShot");
			Goto FireNormalShot;
		AltFire:
		FireNormalShot:
			MBUS A 2;
			MBUS B 6 Bright {
				A_FireBullets(2.85, 2.85, 1, 3, "UnchargedBusterPuff");
				A_PlaySound("weapons/buster/pew", CHAN_WEAPON);
			}
			MBUS C 4 Bright;
			MBUS D 4; 
			Goto Ready;
		FireMidChargeShot:
			MBUS A 0 A_TakeInventory("PrimaryCharge", 100);
		    MBUS A 2;
			MBUS B 6 Bright {
				A_FireBullets(2.85, 2.85, 1, 15, "MidChargeBusterPuff");
				A_PlaySound("weapons/buster/pew", CHAN_WEAPON);
			}
			MBUS C 4 Bright;
			MBUS D 4; 
			Goto Ready;
		FireChargeShot:
			MBUS A 0 A_TakeInventory("PrimaryCharge", 100);
		    MBUS A 2;
			MBUS B 6 Bright {
				A_FireBullets(2.85, 2.85, 1, 30, "ChargedBusterPuff");
				A_PlaySound("weapons/buster/pew", CHAN_WEAPON);
			}
			MBUS C 4 Bright;
			MBUS D 4; 
			Goto Ready;
		Spawn:
			SHTG A -1;
			Stop;
			
			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",100,"ChargeSoundFunction");
			//Upon reaching 99 charge, play the charged sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",99,"ChargeSound2");
			//With 5 or more, stay silent.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",3,"ChargeSound1");
			//If there's any charge, restart.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;
			
			ChargeSound1:
			TNT1 A 0 A_PlaySound("weapons/buster/charging", CHAN_WEAPON);
			goto ChargeSoundFunction;
			
			ChargeSound2:
			TNT1 A 0 A_PlaySound("weapons/buster/charged", CHAN_WEAPON);
			goto ChargeSoundFunction;
	}
}

class BusterPuffBase : Actor
{
	Default
	{
		SeeSound "weapons/hit";
		+NOBLOCKMAP
		+NOGRAVITY
		+ALLOWPARTICLES
		+PUFFONACTORS
	}
	States
	{
		Spawn:
			PUFF ABCD 4 Bright;
			Stop;
	}
}

class UnchargedBusterPuff : BusterPuffBase
{
	Default
	{
		Obituary "%o was tickled to death by %k's uncharged buster shots.";
	}
	States
	{
		Spawn:
			BNP0 ABCD 1 Bright;
			Stop;
	}
}

class MidChargeBusterPuff : BusterPuffBase
{
	Default
	{
		Obituary "%o was sniped by %k's mid-charge shot.";
	}
	States
	{
		Spawn:
			BNP1 AB 1 Bright;
			BNP1 CDE 2 Bright;
			BNP1 F 3 Bright;
			Stop;
	}
}

class ChargedBusterPuff : BusterPuffBase
{
	Default
	{
		Obituary "%o was blasted by %k's charge shot.";
	}
	States
	{
		Spawn:
			BNP2 AB 1 Bright;
			BNP2 CDE 2 Bright;
			BNP2 F 3 Bright;
			Stop;
	}
}