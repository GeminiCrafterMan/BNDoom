class ShanghaiBuster : MegaBuster
{
	Default
	{
		Tag "Shanghai Buster";
	}
	States
	{
		Ready:
			MBUS A 1 A_WeaponReady;
			Loop;
		Deselect:
			MBUS A 1 A_Lower;
			Loop;
		Select:
			MBUS A 1 A_Raise;
			Loop;
		Fire:
			MBUS A 0 {
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
			}
			Charge:
			MBUS A 1 A_GiveInventory("PrimaryCharge", 1);
			MBUS A 0 A_Refire("Charge");
			MBUS A 0 A_JumpIfInventory("PrimaryCharge", 50, "FireChargeShot");
			Goto FireNormalShot;
		AltFire:
		FireNormalShot:
			MBUS A 1 A_SetInventory("PrimaryCharge", 0);
			MBUS B 1 Bright {
				//A_FireBullets(2.85, 2.85, 1, 3, "UnchargedBusterPuff", FBF_NORANDOM, 8192, "UnchargedBusterTracer", 0, 4);
				A_FireProjectile("UnchargedBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/sh-pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			MBUS C 1 Bright;
			MBUS D 1 Bright;
			MBUS E 1;
			Goto Ready;
		FireChargeShot:
			MBUS A 1 A_SetInventory("PrimaryCharge", 0);
			MBUS B 1 Bright {
//				A_FireBullets(2.85, 2.85, 1, 30, "MaxChargeBusterPuff", FBF_NORANDOM, 8192, "MaxChargeBusterTracer", 0, 4);
				A_FireProjectile("MaxChargeBusterTracer", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_PlaySound("weapons/buster/sh-pew", CHAN_WEAPON);
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_SetInventory("ChargingSoundPlayed", 0);
				A_SetInventory("ChargedSoundPlayed", 0);
			}
			MBUS C 1 Bright;
			MBUS D 1 Bright;
			MBUS E 1;
			Goto Ready;
		Spawn:
			SHTG A -1;
			Stop;
			
			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",50,"ChargeSoundFunction");
			//Upon reaching 99 charge, play the charged sound
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",49,"ChargeSound2");
			//With 5 or more, stay silent.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",3,"ChargeSound1");
			//If there's any charge, restart.
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;
			
			ChargeSound1:
				TNT1 A 0 A_PlaySound("weapons/buster/sh-charging", CHAN_WEAPON);
				goto ChargeSoundFunction;
			
			ChargeSound2:
				TNT1 A 0 A_PlaySound("weapons/buster/sh-charged", CHAN_WEAPON);
				goto ChargeSoundFunction;
		
			// Subroutine for charge effect
		ChargeEffectFunction:
			TNT1 A 1;
			// if charge is full, play fully-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",50,"FullChargeAnim");
			// if charge is starting, play not-charged animation
			TNT1 A 0 A_Jumpifinventory("PrimaryCharge",1,"StartChargeAnim");
			stop;

			FullChargeAnim:
				BCH0 UV 1 Bright;
				BCH0 W 2 Bright;
				BCH0 XY 1 Bright;
				BCH0 Z 2 Bright;
				BCH1 AB 1 Bright;
				BCH1 C 2 Bright;
				BCH1 DE 1 Bright;
				BCH1 F 2 Bright;
				goto ChargeEffectFunction;

			StartChargeAnim:
				BCH0 ABCDEFGH 2 Bright;
				goto ChargeEffectFunction;
	}
}