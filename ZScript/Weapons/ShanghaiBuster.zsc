class ShanghaiBuster : BusterBase
{
    action void A_SetElementTranslation(int flags = 0) 
    {
        // Cast and null-check the class:
        let shai = ShanghaiPlayer(self);
        if (!shai)
            return;

        A_OverlayTranslation(OverlayID(), ShanghaiPlayer.ElementTranslations[shai.curElement]);
    }
	action void A_EmotionCheck(int flags = 0)
	{
        // Cast and null-check the class:
        let shai = ShanghaiPlayer(self);
        if (!shai)
            return;

		switch (shai.curEmotion) {
			case ShanghaiPlayer.Emotion_Evil:
			{
				A_OverlayTranslation(99, "DarkSoul");
				A_OverlayRenderStyle(99, STYLE_SUBTRACT);
				A_OverlayAlpha(99, 0.50);
				break;
			}
			case ShanghaiPlayer.Emotion_FullSynchro:
			{
				A_OverlayTranslation(99, "FullSynchro");
				A_OverlayRenderStyle(99, STYLE_ADD);
				A_OverlayAlpha(99, 0.25);
				break;
			}
			default:
				break;
		}
	}
	action void A_FireChargeShot(int flags = 0)
	{
        // Cast and null-check the class:
        let shai = ShanghaiPlayer(self);
        if (!shai)
            return;
		
		switch (shai.curElement) {	// will be shai.curStyleType when we get styles properly working
			case ShanghaiPlayer.Elem_Electric:	// temp
			{
//				A_FireProjectile("ThunderBall");
				A_FireProjectile("ElChain3");
				A_StartSound("weapons/thunder", CHAN_WEAPON);
				break;
			}
			default:
			{
				A_FireProjectile("MaxChargeBusterTracerSH", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_StartSound("weapons/buster/sh-pew", CHAN_WEAPON);
				A_Overlay(1000, "ChargedFlash");	// Flash layer
				break;
			}
		}
	}
	Default
	{
		Tag "Shanghai Buster";
	}
	States
	{
		Ready:
			NBST A 1 {
				A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD);
				A_SetElementTranslation();
			}
			Loop;
		Select:
			TNT1 A 0 A_Overlay(99,"LayerChecker");
			TNT1 A 0 A_SetElementTranslation();	// Set translation
			NBST A 1 A_Raise;
			Loop;
		Fire:
			NBST A 0 {
				A_OverlayFlags(5, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_OverlayFlags(-1, PSPF_POWDOUBLE|PSPF_CVARFAST, true);
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
			}
			Charge:
				NBST A 1 A_GiveInventory("PrimaryCharge", 1);
				NBST A 0 A_Refire("Charge");
				NBST A 0 A_JumpIfInventory("PrimaryCharge", 33, "FireChargeShot");
				Goto FireNormalShot;
		AltFire:
		FireNormalShot:
			NBST A 1 A_SetInventory("PrimaryCharge", 0);
			NBST A 0 A_GiveInventory("Firing", 1);
			NBST B 1 Bright {
				A_FireProjectile("UnchargedBusterTracerSH", frandom(-1.425, 1.425), 0, 4, 0, 0, frandom(-1.425, 1.425));
				A_StartSound("weapons/buster/sh-pew", CHAN_WEAPON);
				A_Overlay(PSP_FLASH, "Flash");	// Flash layer
			}
			Fired:
			NBST A 0 {
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_TakeInventory("Firing", 1);
			}
			NBST C 1 Bright;
			NBST D 1 Bright;
			NBST A 1 A_Refire;	// Move this A_Refire to the previous frame to make it go faster.
			Goto Ready;
		FireChargeShot:	// have to fucking redefine this or else she drops back into mega's colors and charge shot
			NBST A 1 {
				A_SetInventory("PrimaryCharge", 0);
				A_GiveInventory("Firing", 1);
			}
			NBST B 2 Bright A_FireChargeShot;
		// Copy of "Fired" to allow the charge flash to work properly.
			NBST A 0 {
				A_Overlay(5,"ChargeSoundFunction"); //Starts charge sound.
				A_Overlay(-1, "ChargeEffectFunction"); // starts charge effect
				A_TakeInventory("Firing", 1);
			}
			NBST C 2 Bright;
			NBST D 1 Bright A_Refire;
			NBST A 1;
			Goto Ready;

		Flash:
			BSFL ABCD 1 Bright A_SetElementTranslation;
			Stop;

		ChargedFlash:
			BSFL ABC 1 Bright A_SetElementTranslation;
			BSFL BCD 1 Bright A_OverlayTranslation(OverlayID(), "SHChargeFlash");
			Stop;

			//Subroutine for charge sound
		ChargeSoundFunction:
			TNT1 A 1;
			//If charge is full, don't play any sound
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",33,"ChargeSoundFunction");
			//Upon reaching 99 charge, play the charged sound
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",32,"ChargeSound2");
			//With 5 or more, stay silent.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",4,"ChargeSoundFunction");
			//Upon reaching 3 charge, play chargesound1
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",3,"ChargeSound1");
			//If there's any charge, restart.
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",1,"ChargeSoundFunction");
			stop;
			
			ChargeSound1:
				TNT1 A 0 A_StartSound("weapons/buster/sh-charging", CHAN_WEAPON);
				goto ChargeSoundFunction;
			
			ChargeSound2:
				TNT1 A 0 A_StartSound("weapons/buster/sh-charged", CHAN_WEAPON);
				goto ChargeSoundFunction;
		
			// Subroutine for charge effect
		ChargeEffectFunction:
			TNT1 A 1;
			// if charge is full, play fully-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",33,"FullChargeAnim");
			// if charge is starting, play not-charged animation
			TNT1 A 0 A_JumpIfInventory("PrimaryCharge",1,"StartChargeAnim");
			stop;

			FullChargeAnim:
				BCH2 IJKLMNOP 2 Bright;
				goto ChargeEffectFunction;

			StartChargeAnim:
				BCH2 ABCDEFGH 2 Bright;
				goto ChargeEffectFunction;
		Zoom:
			NBST A 10 {
				// Cast and null-check the class:
				let shai = ShanghaiPlayer(self);
				if (shai) {
					switch (shai.curElement) {
						case ShanghaiPlayer.Elem_Extra:
						{
							shai.curElement = ShanghaiPlayer.Elem_Normal;
							break;
						}
						default:
						{
							shai.curElement++;
							break;
						}
					}
				}
			}
		Reload:
			NBST A 10 {
				// Cast and null-check the class:
				let shai = ShanghaiPlayer(self);
				if (shai) {
					switch (shai.curStyleType) {
						case ShanghaiPlayer.Style_Witch:
						{
							shai.curStyleType = ShanghaiPlayer.Style_None;
							break;
						}
						default:
						{
							shai.curStyleType++;
							break;
						}
					}
				}
			}
	}
}